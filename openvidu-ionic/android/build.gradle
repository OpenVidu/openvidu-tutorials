// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath 'com.google.gms:google-services:4.4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects { subproject ->
    subproject.plugins.withId('com.android.library') {
        subproject.extensions.configure(com.android.build.api.dsl.LibraryExtension) { extension ->
            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_21
                targetCompatibility JavaVersion.VERSION_21
            }

            if (!extension.namespace) {
                switch (subproject.name) {
                    case 'capacitor-android':
                        extension.namespace = 'com.getcapacitor.android'
                        break
                    case 'capacitor-cordova-android-plugins':
                        extension.namespace = 'capacitor.android.plugins'
                        break
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete(rootProject.buildDir)
}
